;Процедуры  FAT Driver. работа со строками
;файл DrvFAT.str.a80
;
;strSetExtToName	установка заданного расширения в имя файла
;RenewPath		переход в другой каталог (модификация строки пути к каталогу)
;strHddPart		выделение номера винчестера и раздела из строки
;FileName2NameExt	копирование имени файла из формата 8+3 в формат name.ext
;FileNameLFNto8dot3	перевод имени файла из формата name.ext в формат 8+3
;			(для записи короткого имени в дескриптор)
;FileNameTo8dot3	перевод имени файла из формата name.ext в формат 8+3
;			(для записи в дескриптор)
;strCopyName		формируем имя файла/расширения длиной не более B, до
;			точки или до конца строки
;TestPathFile		проверка пути/имени к файлу на допустимость символов
;			с переводом букв в верхний регистр
;strTestFileName	проверка имени файла на допустимость символов с
;			переводом букв в верхний регистр
;strTestNameLFN		проверка на допустимость символа в имени LFN
;chrCheckLFN		проверка на допустимость символа имени LFN
;SkipSeparator		пропуск идущих подряд разделителей имен в пути
;toUpCaseHL		перевод символов [a..z], [а..я] в строке в верхний
;			регистр, удаление дублирующих разделителей
;
;------------------------------------------------------------------------------
strSetExtToName	ifused
;установка заданного расширения в имя файла
;  имя должно быть проверено и переведено в верхний регистр
;  символ конца строки <#20
;вх:  hl - адрес строки с именем файла в формате 8.3
;     de - адрес строки с расширением
;вых: de - адрес конца имени файла
;
;strSetExtToName
;
	push	hl
	push	bc
loop052	ld	a,(hl)
	inc	hl
	cp	#20
	jr	c,goto138	;конец имени
	cp	"."
	jr	nz,loop052	;дальше расширение
goto138	dec	hl
	ld	(hl),"."
	inc	hl
	ex	de,hl
	ldi
	ldi
	ldi
	xor	a
	ld	(de),a
	pop	bc
	pop	hl
	ret

	endif

;------------------------------------------------------------------------------
RenewPath	ifused
;переход в другой каталог (модификация строки пути к каталогу)
;вх:  hl - адрес дескриптора файла
;     7,e =0 это файл/каталог
;         =1 это возврат в родительский каталог
;     0,e =1 это длинное имя
;вых: a - новая длина пути
;
;RenewPath
;
	push	hl
	bit	7,e
	ex	de,hl
	ld	hl,CurrentPath
	ld	c,(hl)		;длина пути
	ld	b,#00
	add	hl,bc
	jr	nz,goto121	;переход в родительский подкаталог

;вход в каталог (добавление директории к текущему пути)
	ex	de,hl
	call	FileName2NameExt
	ex	de,hl
	ld	(hl),fnSep	;"/" символ разделитель
goto122	inc	hl
	ld	(hl),#00
	ld	bc,CurrentPath
	or	a
	sbc	hl,bc
	ld	a,l
	ld	(bc),a		;новая длина пути
	pop	hl
	ret

;переход в родительский подкаталог
goto121	dec	hl
loop048	dec	hl
	ld	a,(hl)
	cp	fnSep		;"/"
	jr	nz,loop048
	jr	goto122

	endif

;------------------------------------------------------------------------------
strHddPart	ifused
;выделение номера винчестера и раздела из строки
;вх:  hl - адрес строки, содержащей путь к файлу в asciz
;        формат имени файла: [hd{0|1},{0|1|2|3}:]\[DIR\DIR\..\DIR\]filename.ext
;вых: cy=1 - успешно
;        c - номер винчестера [0..1]
;        b - номер раздела на нем [0..3]
;        a - заданный в строке раздел
;     cy=0 - раздел в строке отсутствует
;        a - текущий раздел
;     hl - начало пути к файлу, без номера винчестера
;
;strHddPart
;
;выделение номера винчестера и раздела из строки
	push	hl
	ld	a,(hl)
	and	#5F
	cp	"H"
	jr	nz,goto055	;раздел не указан
	inc	hl
	ld	a,(hl)
	and	#5F
	cp	"D"
	jr	nz,goto055	;раздел не указан
	inc	hl
	ld	c,(hl)		;номер винчестера
	inc	hl
	ld	a,(hl)		;разделитель. должна быть запятая
	cp	","
	jr	nz,goto055	;раздел не указан
	inc	hl
	ld	b,(hl)		;номер раздела
	inc	hl
	ld	a,":"
	cp	(hl)
	jr	nz,goto055	;раздел не указан
	inc	hl

;выделение номера раздела
	ld	a,b
	sub	"0"
	jr	c,goto055	;nz несуществующий раздел
	cp	#04
	ld	b,a		;номер раздела [0..3]
	jr	nc,goto055	;nz несуществующий раздел

;выделение номера винчестера
	ld	a,c
	sub	"0"
	jr	c,goto055	;nz несуществующий раздел
	IFDEF	ZC
	cp	#03
	ELSE
	cp	#02
	ENDIF
	jr	nc,goto055	;nz несуществующий раздел
	ld	c,a
	add	a,a
	add	a,a
	or	b
	ex	(sp),hl
	pop	hl
	scf
	ret	

;винчестер и раздел в строке не обнаружены
goto055	xor	a
	pop	hl
	ld	a,(PartitionNum)
	ret

	endif

;------------------------------------------------------------------------------
FileName2NameExt	ifused
;копирование имени файла из формата 8+3 в формат name.ext
;вх:  hl - адрес дескриптора файла (имя в формате 8+3)
;     de - адрес приемника
;вых: de - превый свободный байт приемника
;     hl=hl+#0B (адрес атрибутов в дескрипторе)
;
;FileName2NameExt
;
	ld	a,#08
	call	proc_09
	ld	a,(hl)		;добавим расширение, если есть
	cp	" "
	ret	z		;расширения нет
	ld	a,"."
	ld	(de),a		;разделитель
	inc	de
	ld	a,#03
proc_09	ld	b,#00
	ld	c,a
	ldir
	ld	b,a
loop047	dec	de		;удалим последние пробелы
	ld	a,(de)
	inc	de
	cp	" "
	ret	nz
	dec	de
	djnz	loop047
	ret

	endif

;------------------------------------------------------------------------------
FileNameLFNto8dot3	ifused
;перевод имени файла из формата name.ext в формат 8+3(для записи короткого
;  имени в дескриптор)
;  символ конца строки <#20, разделитель между каталогами fnSep
;вх:  hl - адрес имени в формате [\]name[.][ext]\
;     de - адрес приемника имени
;вых: hl - адрес следующего имени в пути
;     b=#00
;
;FileNameLFNto8dot3
;
	call	SkipSeparator

;формируем имя файла
	push	de
	ld	bc,#087F
	call	strCopyName

;ищем точку начало расширения
loop060	ld	a,(hl)
	cp	#20
	jr	c,goto158		;конец строки
	cp	fnSep			;"/"
	jr	z,goto158		;конец имени
	inc	hl
	cp	"."
	jr	nz,loop060

;формируем расширение файла
goto158	ld	b,#03
	call	strCopyName

;переведем в верхний регистр
	ex	(sp),hl
	ld	a,(de)
	push	af
	xor	a
	ld	(de),a
	call	toUpCaseHL
	pop	af
	ld	(de),a
	pop	hl

	ret

	endif

;------------------------------------------------------------------------------
FileNameTo8dot3	ifused
;перевод имени файла из формата name.ext в формат 8+3(для записи в дескриптор)
;  имя должно быть проверено и переведено в верхний регистр
;  символ конца строки <#20, разделитель между каталогами fnSep
;вх:  hl - адрес имени в формате [\]name[.][ext]\
;     de - адрес приемника имени
;вых: hl - адрес следующего имени в пути
;
;FileNameTo8dot3
;
	call	SkipSeparator

;формируем имя файла/расширения 
	ld	bc,#08FF
	call	strCopyName
	ld	a,(hl)
	cp	"."
	jr	nz,goto062
	inc	hl

;формируем расширение файла
goto062	ld	b,#03
	jr	strCopyName
	org	$-2

/* старая версия
;FileNameTo8dot3
;
	ld	bc,#08FF
	ld	a,(hl)
	cp	fnSep		;"/"
	jr	nz,loop022
	inc	hl

;формируем имя файла 
loop022	ld	a,(hl)
	cp	#20
	jr	c,goto060	;конец пути
	cp	fnSep		;"/"
	jr	z,goto060	;конец имени
	cp	"."
	jr	z,goto061	;дальше расширение
	ldi
	djnz	loop022
	ld	a,(hl)
	cp	"."
	jr	nz,goto062
	inc	hl
	jr	goto062
goto061	inc	hl
;  заполняем остаток имени пробелами
goto060	ld	a," "
loop023	ld	(de),a
	inc	de
	djnz	loop023

;формируем расширение файла
goto062	ld	b,#03
loop024	ld	a,(hl)
	cp	#20
	jr	c,goto064	;конец пути
	cp	fnSep		;"/"
	jr	z,goto064	;конец имени
	ldi
	djnz	loop024
	ret
;  заполняем остаток расширения пробелами
goto064	ld	a," "
loop025	ld	(de),a
	inc	de
	djnz	loop025
	ret
*/

	endif

;------------------------------------------------------------------------------
strCopyName	ifused
;формируем имя файла/расширения длиной не более B, до точки или до конца строки
;вх:  hl - адрес строки с именем источника
;     de - адрес строки приемника
;     b - длина имени/расширения
;     c=#7F/#FF для длинных имен/для имен 8.3 или 8+3
;вых: hl - указывает на конец строки/имени/точку/следующий символ имени
;     de - сформированное имя дополненное пробелами
;     b=#00
;
;strCopyName
;
loop022	ld	a,(hl)
	cp	#20
	jr	c,goto060	;конец пути
	jr	nz,goto176
	bit	7,c
	inc	hl
	jr	z,loop022	;пробел пропускаем
	dec	hl
goto176	cp	fnSep		;"/"
	jr	z,goto060	;конец имени
	cp	"."
	jr	z,goto060	;дальше расширение
	ldi
	djnz	loop022
	ret

;заполняем остаток имени пробелами
goto060	ld	a," "
loop023	ld	(de),a
	inc	de
	djnz	loop023
	ret

	endif

;------------------------------------------------------------------------------
TestPathFile	ifused
;проверка пути/имени к файлу на допустимость символов с переводом букв в верхний
;  регистр, с заменой разделителей на /
;  символ конца строки <#20, разделитель между каталогами \ или /
;вх:  hl - адрес начала пути
;вых: cy=1 путь содержит недопустимые символы, либо слишком длинный
;     cy=0 синтаксичесая проверка пройдена
;       z - путь пустой -> a,e=#00
;       nz - путь не пустой 
;         a - вложенность (a=#00 только имя файла в пути)
;         b=c=#00 путь заканчивается разделителем
;
;TestPathFile
;
	call	toUpCaseHL

;частный случай. пустое имя
	ld	e,#00		;вложенность
	ld	a,(hl)
	cp	#20
	jr	c,goto133

;пропустим первый разделитель
	push	hl
	call	SkipSeparator
;	cp	fnSep		;"/"
;	jr	nz,loop032
;	inc	hl

loop032	ld	bc,#0000	;длина имени/расширения
	ld	d,c
loop030	ld	a,(hl)
	cp	#20
	jr	c,goto075	;конец пути
	cp	fnSep		;"/"
	jr	z,goto076	;конец имени файла/каталога
	cp	"."
	jr	nz,goto077
	inc	d
	dec	d
	jr	nz,goto077	;уже обработка расширения
	inc	d		;первая точка в имени
	ld	b,c		;длина имени
	ld	c,#00		;теперь считаем расширение
	inc	hl
	jr	loop030
goto077	call	proc_11		;проверка на допустимость символа
	jr	nz,loop030	;допустимый символ в имени файла

;недопустимый смвол в имени
goto078	scf
	pop	hl
	ret

;пропускаем идущие подряд разделители пути
;b - длина имени
;c - длина расширения
goto076	call	proc_06
	jr	c,goto078	;недопустимый размер имени
	inc	e
	inc	hl
	jr	loop032

;конец строки, проверим чем заканчивается строка
goto075	dec	hl
	ld	a,(hl)
	pop	hl
	cp	fnSep		;"/"
	jr	z,goto133	;проверим только вложенность

;проверка параметров имени и пути
;вх:  d=1 есть имя и расширение
;      =0 есть только имя
;     b - длина имени
;     c - длина расширения
;     e - вложенность пути
;вых: cy=1 путь содержит недопустимые символы, либо слишком длинный
proc_06	ld	a,d
	or	a
	jr	nz,goto080
	ld	b,c
	ld	c,a
goto080	ld	a,b
	or	a
	scf
	ret	z		;пустое имя недопустимо
	cp	#09
	ccf
	ret	c		;имя более 8 символов недопустимо
	ld	a,c
	cp	#04
	ccf
	ret	c		;расширение более 3 символов недопустимо
goto133	ld	a,e
	or	a
	ret	z		;nc,z - пустой путь
	and	#7F	
	cp	#10
	ccf
	ret			;nz

;проверка на допустимость символа
proc_11	inc	c
	set	7,e
proc_18	push	hl
	push	bc
	ld	hl,IllegalSym
	ld	bc,endIllegalSym-IllegalSym
	cpir
	pop	bc
	pop	hl
	inc	hl
	ret

	endif

;------------------------------------------------------------------------------
strTestFileName	ifused
;проверка имени файла на допустимость символов с переводом букв в верхний
;  регистр
;  символ конца строки <#20
;вх:  hl - адрес начала имени
;вых: cy=1 имя содержит недопустимые символы, либо слишком длинный, или пустой
;     cy=0 синтаксичесая проверка пройдена
;
;strTestFileName
;
	call	toUpCaseHL

;частный случай. пустое имя
	ld	a,(hl)
	cp	#20
	ret	c

;сравним имя
	push	hl
	ld	de,#0001
	ld	b,d		;длина имени/расширения
	ld	c,d
loop051	ld	a,(hl)
	cp	#20
	jr	c,goto075	;конец имени
	cp	"."
	jr	nz,goto079
	inc	d
	dec	d
	jr	nz,goto079	;уже обработка расширения
	inc	d		;первая точка в имени
	ld	b,c		;длина имени
	ld	c,#00		;теперь считаем расширение
	inc	hl
	jr	loop051
goto079	call	proc_11		;проверка на допустимость символа
	jr	nz,loop051	;допустимый символ в имени файла

;недопустимый смвол в имени
	scf
	pop	hl
	ret
	jp	TestPathFile
	org	$-3

	endif

;------------------------------------------------------------------------------
strTestNameLFN	ifused
;проверка на допустимость символа в имени LFN
;  символ конца строки <#20
;вх:  hl - строка с именем
;вых: cy=1 недопустимое имя
;     a - не определен
;     другие регистры не меняются
;
;strTestNameLFN
;
;частный случай. пустое имя
	ld	a,(hl)
	cp	#20
	ret	c

;проверка на допустимость символа
	push	hl
loop075	call	chrCheckLFN
	jr	z,goto202		;найден недопустимый символ
	inc	hl
	ld	a,(hl)
	cp	#20
	jr	nc,loop075
goto202	pop	hl
	ccf
	ret

	endif

;------------------------------------------------------------------------------
chrCheckLFN	ifused
;проверка на допустимость символа имени LFN
;вых: z - не допустимый символ в имени файла
;
;chrCheckLFN
;
	push	hl
	push	bc
	ld	bc,endIllegalSym-IllegalLFN
	ld	hl,IllegalLFN
	cpir
	pop	bc
	pop	hl
	ret

	endif

;------------------------------------------------------------------------------
SkipSeparator	ifused
;пропуск идущих подряд разделителей имен в пути
;вх:  hl - адрес в пути, указывающий на разделитель
;вых: hl - адрес начала имени в пути
;
;SkipSeparator
;
loop031	ld	a,(hl)
	cp	fnSep1		;"/"
	jr	z,goto063
	cp	fnSep2		;"\"
	ret	nz
goto063	inc	hl
	jr	loop031

	endif

;------------------------------------------------------------------------------
toUpCaseHL	ifused
;перевод символов [a..z], [а..я] в строке в верхний регистр, удаление
;  дублирующих разделителей
;вх:  hl - адрес строки символов
;
;toUpCaseHL
;
	push	de
	push	hl

	dec	hl
	ld	d,h
	ld	e,l

loop050	inc	hl
loop021	inc	de
	ld	a,(hl)
	ld	(de),a
	cp	#20
	jr	c,goto051
	cp	fnSep2		;"\"
	jr	z,goto131
	cp	fnSep1		;"/"
	jr	z,goto131
	cp	"a"
	jr	c,loop050
	cp	"z"+1
	jr	c,goto052
	cp	#A0		;rus а
	jr	c,loop050
	cp	#B0		;rus п
	jr	c,goto052
	cp	#E0		;rus р
	jr	c,loop050
	cp	#F0		;rus я
	jr	c,goto053
	cp	#F1		;rus ё
	jr	nz,loop050
	dec	a
	jr	goto054
goto053	and	#9F
goto052	and	#DF
goto054	ld	(de),a
	jr	loop050

goto051	pop	hl
	pop	de
	ret

goto131	call	SkipSeparator
	ld	a,fnSep		;"/"
	ld	(de),a
	jr	loop021

	endif

;==============================================================================
;недопустимые символы в имени файла
IllegalSym	db " ."
		db "+[]=,"
		db #7F,#3B
IllegalLFN	db "/:*?<>|",#22,#5C
endIllegalSym
;==============================================================================
